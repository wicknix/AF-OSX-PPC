apply plugin: 'java'

/**
 * This task runs when any input file is newer than the omnijar.
 */
task buildOmnijar(type:Exec) {
    dependsOn generateCodeAndResources

    // Depend on all the Gecko resources.
    inputs.sourceDir 'src/main/java/locales'
    inputs.sourceDir 'src/main/java/chrome'
    inputs.sourceDir 'src/main/java/components'
    inputs.sourceDir 'src/main/java/modules'
    inputs.sourceDir 'src/main/java/themes'

    // Produce a single output file.
    outputs.file "${topobjdir}/dist/fennec/assets/omni.ja"

    workingDir "${topobjdir}"

    commandLine "${topsrcdir}/mach"
    args 'build'
    args '-C'
    args 'mobile/android/base'
    args 'gradle-omnijar'

    // Only show the output if something went wrong.
    ignoreExitValue = true
    standardOutput = new ByteArrayOutputStream()
    errorOutput = standardOutput
    doLast {
        if (execResult.exitValue != 0) {
            throw new GradleException("Process '${commandLine}' finished with non-zero exit value ${execResult.exitValue}:\n\n${standardOutput.toString()}")
        }
    }
}

apply plugin: 'idea'

idea {
    module {
    }
}
